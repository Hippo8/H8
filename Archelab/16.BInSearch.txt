La ricerca dicotomica è un efficiente algoritmo che trova un dato elemento in un array ordinato. Ad ogni iterazione l'algoritmo confronta l'elemento in input con l'elemento che si trova nella posizione centrale della parte dell'array da visitare. Se tale elemento è uguale all'elemento di input l'algoritmo termina con successo. Se tale elemento è minore dell'elemento di input si continua la ricerca considerando solo la prima metà dell'array, in caso contrario si considera la seconda metà dell'array. L'algoritmo termina quando si è trovato l'elemento che si sta cercando oppure non è possibile continuare perché si è certi che l'elemento non si trova nell'array.

Si implementi la ricerca dicotomica in RISC-V. Di seguito trovate la funzione in C che implementa tale algoritmo.

int binsearch(int array[], int size, int needle) {
    int low = 0;
    int high = size - 1;

    while (low <= high) {
        int mid = (low + high) / 2;

        if (array[mid] == needle) {
            return mid;
        } else if (array[mid] < needle) {
            low = mid + 1;
        } else {
            high = mid - 1;
        }
    }
    // needle not found
    return -1;
}

Attenzione:
Incollare nel campo di sotto soltanto il contenuto della funzione binsearch
 (in RISC-V)
Attenzione alle convenzioni di chiamata!
Usare il seguente codice "main" per sviluppo e debugging nel simulatore RARS
######################		CODE		####################
################################################################################
# Procedure binsearch(v, n, x)
# a0 -> v address
# a1 -> v size
# a2 -> x
################################################################################
binsearch:
    addi    t1, zero, 0  # left = 0
    addi    t2, a1, -1   # right = size - 1
    add     t3, zero, -1 # return value
    
binsearch_loop: # while loop
    bgt     t1, t2, binsearch_exit   # left > right, break
    add     t0, t1, t2   # mid = left + right
    srai    t0, t0, 1    # mid = (left + right) / 2

    # Get the element at the midpoint
    slli    t4, t0, 2    # Scale the midpoint by 4
    add     t4, a0, t4   # Get the memory address of arr[mid]
    lw      t4, 0(t4)    # Dereference arr[mid]

    # See if the needle (a2) > arr[mid]
    ble     a2, t4, binsearch_left
    # If we get here, then the needle is > arr[mid]
    addi    t1, t0, 1    # left = mid + 1
    j       binsearch_loop
binsearch_left:
    bge     a2, t4, binsearch_found
    # If we get here, then needle < arr[mid]
    addi    t2, t0, -1   # right = mid - 1
    j       binsearch_loop
binsearch_found:
    # If we get here, then needle == arr[mid]
    addi    t3, t0, 0

binsearch_exit:
    addi    a0, t3, 0
    ret


.globl _start
.data
    needle: .word 2
    size:   .word 8
    array:  .word 1,2,3,7,9,10,11,15
.text
_start:
    # chiama binsearch
    la   a0, array
    la   a1, size
    lw   a1, 0(a1)
    la   a2, needle
    lw   a2, 0(a2)
    jal  ra, binsearch
    
    #exit
    li   a7, 10
    ecall
